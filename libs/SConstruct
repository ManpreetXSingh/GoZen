#!/usr/bin/env python
import os
import sys
import platform as os_platform


LIBS_COMMON = ['avcodec', 'avformat', 'avdevice', 'avutil', 'swresample', 'swscale']
MARCH_FLAGS = {
    'x86_64': 'x86-64',
    'arm64': 'native'  # Using 'native' for ARM64 is often safer than specifying specific architecture
}


cache_folder = ARGUMENTS.get('cache_dir', 'null')
if cache_folder != 'null':
    CacheDir(cache_folder)

env = SConscript('godot_cpp/SConstruct')
env.Append(CPPPATH=['gde_gozen'])
env_suffix = env['suffix']
env_shlibsuffix = env['SHLIBSUFFIX']

jobs = ARGUMENTS.get('jobs', 4)
platform = ARGUMENTS.get('platform', 'linux')
arch = ARGUMENTS.get('arch', 'x86_64')
target = ARGUMENTS.get('target', 'template_debug').split('_')[-1]
libpath = f'bin/{platform}_{arch}/libgozen{env_suffix}{env_shlibsuffix}'


if 'linux' in platform:
    env.Append(
        LINKFLAGS=['-static-libstdc++'],
        CPPFLAGS=['-Iffmpeg/bin_linux', '-Iffmpeg/bin_linux/include'],
        LIBPATH=[
            'ffmpeg/bin_linux/include/libavcodec',
            'ffmpeg/bin_linux/include/libavformat',
            'ffmpeg/bin_linux/include/libavdevice',
            'ffmpeg/bin_linux/include/libavutil',
            'ffmpeg/bin_linux/include/libswresample',
            'ffmpeg/bin_linux/include/libswscale',
            'ffmpeg/bin_linux/lib'],
        CCFLAGS=[f'-march={MARCH_FLAGS[arch]}'],
        LIBS=LIBS_COMMON)
elif 'windows' in platform:
    if os_platform.system().lower() == 'windows':
        env.Append(LIBS=[
            'avcodec.lib',
            'avformat.lib',
            'avdevice.lib',
            'avutil.lib',
            'swresample.lib',
            'swscale.lib'])
    else:
        env.Append(LIBS=LIBS_COMMON)

    env.Append(
        CPPPATH=['ffmpeg/bin_windows/include'],
        LIBPATH=['ffmpeg/bin_windows/bin'])
elif 'macos' in platform:
    # MacOS can only be build on a MacOS machine!
    macos_path = f'bin/{platform}/{target}/lib'

    env.Append(
        CPPPATH=['ffmpeg/bin_macos/include'],
        LIBPATH=[
            'ffmpeg/bin_macos/lib',
            'ffmpeg/bin_macos/include/libavcodec',
            'ffmpeg/bin_macos/include/libavformat',
            'ffmpeg/bin_macos/include/libavdevice',
            'ffmpeg/bin_macos/include/libavutil',
            'ffmpeg/bin_macos/include/libswresample',
            'ffmpeg/bin_macos/include/libswscale',
            macos_path,
            '/usr/local/lib'],
        LIBS=LIBS_COMMON,
        LINKFLAGS=[
            '-stdlib=libc++',
            '-framework', 'CoreFoundation',
            '-framework', 'CoreVideo',
            '-framework', 'CoreMedia',
            '-framework', 'AVFoundation',
            '-rpath', 'libPath'])
    libpath = f'bin/{platform}_{arch}/{target}/libgozen{env_suffix}{env_shlibsuffix}'


src = Glob('gde_gozen/*.cpp')
sharedlib = env.SharedLibrary(libpath, src)
Default(sharedlib)
